plugins {
    id "fabric-loom" version "1.11-SNAPSHOT"
    id "com.modrinth.minotaur" version "2.+"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "maven-publish"
}

base.archivesName = project.archives_base_name
version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    // Minecraft 1.20.5 (24w14a) upwards uses Java 21.
    it.options.release = 21
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    
    withSourcesJar()
}

jar {
    from "LICENSE.txt"
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    repositories {
        // mavenLocal()
    }
}

def ENV = System.getenv()

modrinth {
    token = ENV.MODRINTH_TOKEN

    projectId = "TvL1V8O5"
    versionNumber = "${project.mod_version}+${project.minecraft_version}"
    versionName = "v${project.mod_version} [${project.minecraft_version}]"
    versionType = "release"
    changelog = "A changelog can be found at https://github.com/Grayray75/NoRecipeBook/blob/main/CHANGELOG.md"
    
    uploadFile = remapJar
    gameVersions = ["1.21.2", "1.21.3", "1.21.4", "1.21.5", "1.21.6", "1.21.7", "1.21.8", "1.21.9", "1.21.10"]
    loaders = ["fabric", "quilt"]
}

curseforge {
    if (ENV.CURSEFORGE_TOKEN) {
        apiKey = ENV.CURSEFORGE_TOKEN
    }

    project {
        id = "491407"
        releaseType = "release"
        changelog = "A changelog can be found at https://github.com/Grayray75/NoRecipeBook/blob/main/CHANGELOG.md"

        addGameVersion "1.21.2"
        addGameVersion "1.21.3"
        addGameVersion "1.21.4"
        addGameVersion "1.21.5"
        addGameVersion "1.21.6"
        addGameVersion "1.21.7"
        addGameVersion "1.21.8"
        addGameVersion "1.21.9"
        addGameVersion "1.21.10"
        addGameVersion "Fabric"
        addGameVersion "Quilt"
        addGameVersion "Java 21"

        mainArtifact(remapJar) {
            displayName = "v${project.mod_version} [${project.minecraft_version}]"
        }

        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }

    options {
        forgeGradleIntegration = false
        javaVersionAutoDetect = false
    }
}
