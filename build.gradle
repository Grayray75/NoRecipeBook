plugins {
    id "fabric-loom" version "1.3-SNAPSHOT"
    id "io.github.juuxel.loom-vineflower" version "1.11.0"
    id "com.modrinth.minotaur" version "2.+"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "maven-publish"
}

base.archivesName = project.archives_base_name
version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
    it.options.release = 17
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    
    withSourcesJar()
}

jar {
    from "LICENSE.txt"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        // mavenLocal()
    }
}

def ENV = System.getenv()

modrinth {
    token = ENV.MODRINTH_TOKEN

    projectId = "TvL1V8O5"
    versionNumber = "${project.mod_version}+1.20"
    versionName = "v${project.mod_version} [1.20.x]"
    versionType = "release"
    changelog = "A changelog can be found at https://github.com/Grayray75/NoRecipeBook/blob/1.20/CHANGELOG.md"
    
    uploadFile = remapJar
    gameVersions = ["1.20", "1.20.1"]
    loaders = ["fabric", "quilt"]
}

curseforge {
    if (ENV.CURSEFORGE_TOKEN) {
        apiKey = ENV.CURSEFORGE_TOKEN
    }

    project {
        id = "491407"
        releaseType = "release"
        changelog = "A changelog can be found at https://github.com/Grayray75/NoRecipeBook/blob/1.20/CHANGELOG.md"

        addGameVersion "1.20"
        addGameVersion "1.20.1"
        addGameVersion "Fabric"
        addGameVersion "Quilt"
        addGameVersion "Java 17"

        mainArtifact(remapJar) {
            displayName = "v${project.mod_version} [1.20.x]"
        }

        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }

    options {
        forgeGradleIntegration = false
        javaVersionAutoDetect = false
    }
}
